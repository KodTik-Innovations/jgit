plugins {
    id 'java-library'
    id 'maven-publish'    
    id 'com.diffplug.spotless' version '6.25.0'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.assemble.dependsOn tasks.shadowJar
tasks.build.dependsOn tasks.shadowJar

spotless {
    java {
        target "**/*.java"		  
        googleJavaFormat()
    }
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

shadowJar {    
    exclude 'META-INF/maven/org.eclipse.jgit/*/pom.xml'
    exclude 'META-INF/maven/org.eclipse.jgit/*/pom.properties'
    exclude 'META-INF/maven/commons-codec/*/pom.xml'
    exclude 'META-INF/maven/commons-codec/*/pom.properties'
    archiveFileName.set("${project.name}.jar")
    
    dependencies {
        exclude(dependency('org.slf4j:slf4j-api:2.1.0-alpha1'))
        exclude(dependency('org.slf4j:slf4j-simple:2.1.0-alpha1'))
        exclude(dependency('com.googlecode.javaewah:JavaEWAH:1.2.3'))
        exclude(dependency('commons-codec:commons-codec:1.17.1'))
    }
}

dependencies {
    implementation 'org.eclipse.jgit:org.eclipse.jgit:7.1.0.202411261347-r'
    implementation 'org.slf4j:slf4j-api:2.1.0-alpha1'
    implementation 'org.slf4j:slf4j-simple:2.1.0-alpha1'
    implementation 'com.googlecode.javaewah:JavaEWAH:1.2.3'
    implementation 'commons-codec:commons-codec:1.17.1'
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->            
            groupId = 'org.kodtik.ide'
            artifactId = 'jgit'
            version = '7.1.0.202411261347-r' 
         
            from project.shadow.component(publication) 
        }
    }
}